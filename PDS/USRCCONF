/* rexx */
/*********************************************************************/
/*                                                                   */
/* This exec will run the CUCI dialog to set ISPF defaults that are  */
/* not included in ISPCCONF.  This is a heavily modified version of  */
/* the ISPF ISPCCONF dialog.  Modifying the existing execs allows us */
/* to reuse common code, such as verifying the keyword file.         */
/*                                                                   */
/* Change Log                                                        */
/*                                                                   */
/*    Date    Name           Description                             */
/* ---------- -------------- --------------------------------------- */
/* 2022/04/26 Conley         Modify code to check that the keyword   */
/*                           dataset name has been customized from   */
/*                           the default.  If not, issue error       */
/*                           message USRL002.                        */
/* 2021/04/13 Conley         Modify code to set variables that will  */
/*                           display the configuraton filename and   */
/*                           the date/time it was created.           */
/* 2020/07/12 Conley         Initial coding.                         */
/*********************************************************************/
/*********************************************************************/
/*                                                                   */
/* Ensure that this exec runs under ISR APPLID.                      */
/*                                                                   */
/*********************************************************************/
address ispexec 'VGET (ZAPPLID) SHARED'
if zapplid <> 'ISR' then
   do
      address ispexec "SELECT CMD(%USRCCONF) NEWAPPL(ISR)"
      exit
   end
address ispexec
'CONTROL ERRORS RETURN'
'VGET (USRVKWD USRVKWDM UCNVOPT) PROFILE'
m = ''
c = 'ZCMD'
local_zcmd = ''
dsrc = 0
display_rc = 0
display_rc2 = 0
Do While display_rc = 0
   if m ¬= ' ' Then
      msg = 'MSG('m')'
   Else
      msg = ''
   If dsrc > 0 Then
      zcmd = local_zcmd
   address ispexec "VGET ( USRVKSRC )"
   if usrvksrc = 'yourhlq.ISPF.KEYWORD(yourmemb)' then
      msg = 'MSG(USRL002)'
   else
      do
         x = outtrap('LISTDS.')
         address tso "LISTDS '"usrvksrc"'"
         listdsrc = rc
         x = outtrap('OFF')
         if listdsrc = 0 then
            do
               parse var listds.7 40 ss 42 .,
                                  54 moddate 59 .,
                                  61 hr 63 .,
                                  63 mn 65 .
               standate = date('S',moddate,'J')
               parse var standate 1 yyyy 5 . 5 mm 7 . 7 dd 9 .
               usrgcmpd = yyyy'/'mm'/'dd
               usrgcmpt = hr':'mn':'ss
            end
      end
   'DISPLAY PANEL(USRPCONF)' msg 'CURSOR('c')'
   display_rc = rc
   m = ''
   c = 'ZCMD'
   local_zcmd = zcmd
   zcmd = ''
   dsrc = 0
   If display_rc = 0 Then
    Do
      'VPUT (USRVKWD USRVKWDM UCNVOPT) PROFILE'
      Select
        When local_zcmd = ' ' Then
          m = 'ISRU292 '
        When local_zcmd = 1 Then
          Do
            m = 'ISPC250'
            Call verify_keyword_file
            If dsrc = 0 Then
              Do
                c = 'ZCMD'
                Address TSO usrcmod
                dsrc = rc
                'VGET (USRVOUTN) SHARED'
                If dsrc > 0 Then
                  m = ''
                If dsrc = 4 Then
                  dsrc = 0
              End
          End
        When local_zcmd = 2 Then
          Do
            Call verify_keyword_file
            If dsrc = 0 Then
              Do
                c = 'ZCMD'
                'CONTROL REFLIST NOUPDATE'
                "LMINIT DATASET("usrvkwd") DATAID(DATA1)"
                'CONTROL REFLIST UPDATE'
                "VIEW DATAID("data1") MEMBER("usrvkwdm")"
                e_rc = rc
                "LMFREE DATAID("data1")"
              End
          End
        Otherwise
          m = 'ISPD241'
      End
    End
End
Exit
/* */
verify_keyword_file:
/* Check target data set */
notfound = 0
c = 'USRVKWD'
If pos('(',usrvkwd) > 0 Then
   Do
      'SETMSG MSG(ISPC282)'
      dsrc=4
   End
If dsrc = 0 Then
   Do
      'CONTROL REFLIST NOUPDATE'
      'LMINIT DATAID(BB) ENQ(SHR) DATASET('usrvkwd')'
      dsrc=rc
      'CONTROL REFLIST UPDATE'
      If dsrc=0 Then
         Do
            'LMOPEN DATAID('bb') OPTION(INPUT) ORG(ORG) LRECL(LRECL)'
            dsrc = rc
            If dsrc = 0 Then
               If org ¬= 'PO' | lrecl < 251 Then
                  Do
                     'SETMSG MSG(ISPC268)'
                     dsrc=4
                  End
               Else
                  If usrvkwdm = '' Then
                     Do
                        c = 'usrvkwdm'
                        'SETMSG MSG(ISRU007)'
                        dsrc=4
                     End
                  Else
                     Do
                        address ispexec "LMMFIND DATAID("bb")",
                                        "MEMBER("usrvkwdm")",
                                        "STATS(NO)"
                        dsrc = rc
                        If dsrc = 8 Then
                           Do
                              notfound = 1
                              dsrc = 0
                           End
                        Else
                           c = 'usrvkwdm'
                     End
         End
   End
If dsrc > 7 Then
   'SETMSG MSG(ISRZ002)'
'LMFREE DATAID('bb')'
'VPUT (NOTFOUND) SHARED'
Return
/* */
